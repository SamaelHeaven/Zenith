plugins {
    id "java-library"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "1.9.22"
    id "org.openjfx.javafxplugin" version "0.0.10"
}

group "org.samaelheaven"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
    mavenLocal()
}

def javafxVersion = "17.0.1"

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

kotlin {
    jvmToolchain(17)
}

javafx {
    version = javafxVersion
    modules = ["javafx.controls", "javafx.fxml"]
}

dependencies {
    for (os in ["win", "linux", "mac"]) {
        runtimeOnly "org.openjfx:javafx-base:$javafxVersion:$os"
        runtimeOnly "org.openjfx:javafx-graphics:$javafxVersion:$os"
        runtimeOnly "org.openjfx:javafx-controls:$javafxVersion:$os"
    }
}

publishing {
    publications {
        gpr(MavenPublication) {
            artifactId="zenith"
            from(components.java)
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/samaelheaven/zenith")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME_GITHUB")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN_GITHUB")
            }
        }
    }
}